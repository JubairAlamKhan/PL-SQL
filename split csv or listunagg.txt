
---->> Solution one

--STEP 1 OBJECT AND TABLE TYPE CREATION  

CREATE TYPE T_ROW AS OBJECT (A VARCHAR2 (50));

CREATE TYPE T_ROW_TAB AS TABLE OF T_ROW;

--STEP 2 MAKING FUNCTION

CREATE OR REPLACE FUNCTION SPLIT (P_CSV_COL VARCHAR2)
   RETURN T_ROW_TAB
IS
   LV_NO_LIST   T_ROW_TAB := T_ROW_TAB ();
BEGIN
   FOR I IN (    SELECT REGEXP_SUBSTR (P_CSV_COL,
                                       '[^,]+',
                                       1,
                                       LEVEL)
                           AS COL_VAL
                   FROM DUAL
             CONNECT BY REGEXP_SUBSTR (P_CSV_COL,
                                       '[^,]+',
                                       1,
                                       LEVEL)
                           IS NOT NULL)
   LOOP
      LV_NO_LIST.EXTEND;
      LV_NO_LIST (LV_NO_LIST.LAST) := T_ROW (I.COL_VAL);
   END LOOP;

   RETURN LV_NO_LIST;
END;
/ 

--- STEP 3 CHECKING FUNCTION
SELECT * 
FROM TABLE (SPLIT ('1,3,4,5,A,B,C,10-SEP-22,10/10/22'));


---->> Solution two



--STEP 1 CREATION OF FUNCTION

CREATE OR REPLACE FUNCTION SPLIT_STRING (I_STR     IN VARCHAR2,
                                         I_DELIM   IN VARCHAR2 DEFAULT ',')
   RETURN SYS.ODCIVARCHAR2LIST
   DETERMINISTIC
AS
   P_RESULT         SYS.ODCIVARCHAR2LIST := SYS.ODCIVARCHAR2LIST ();
   P_START          NUMBER (5) := 1;
   P_END            NUMBER (5);
   C_LEN   CONSTANT NUMBER (5) := LENGTH (I_STR);
   C_LD    CONSTANT NUMBER (5) := LENGTH (I_DELIM);
BEGIN
   IF C_LEN > 0
   THEN
      P_END := INSTR (I_STR, I_DELIM, P_START);

      WHILE P_END > 0
      LOOP
         P_RESULT.EXTEND;
         P_RESULT (P_RESULT.COUNT) := SUBSTR (I_STR, P_START, P_END - P_START);
         P_START := P_END + C_LD;
         P_END := INSTR (I_STR, I_DELIM, P_START);
      END LOOP;

      IF P_START <= C_LEN + 1
      THEN
         P_RESULT.EXTEND;
         P_RESULT (P_RESULT.COUNT) :=
            SUBSTR (I_STR, P_START, C_LEN - P_START + 1);
      END IF;
   END IF;

   RETURN P_RESULT;
END;
/

--STEP 2 CHECKING FUNCTION

SELECT * 
FROM TABLE (SPLIT_STRING ('1,3,4,5,A,B,C,10-SEP-22,10/10/22'));




